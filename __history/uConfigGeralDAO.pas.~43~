unit uConfigGeralDAO;

interface

uses
  uConfigGeral, uConexao, System.SysUtils, FireDAC.Comp.Client, FireDAC.Stan.Param,
  FireDAC.DApt, uUtils;

type
  TConfigGeralDAO = class
  public
    function AlterarConfiguracao(AColuna: String; ANovoValor: Variant): Boolean;
    function InsereRegistroDefault: Boolean;
    class function UpdateInstanciaConfig: TConfigGeral;
  end;

implementation

function TConfigGeralDAO.InsereRegistroDefault: Boolean;
var
  Qry: TFDQuery;
begin
  Result := False;
  Qry := TFDQuery.Create(nil);

  try
    Qry.Connection := dmConexao.FDConnection;
    Qry.SQL.Text := 'SELECT * FROM configuracao ORDER BY id LIMIT 1';
    Qry.Open;

    if Qry.IsEmpty then
    begin
      Qry.Close;
      Qry.SQL.Text := 'INSERT INTO configuracao(valorsalario, flagparcelado, percentualadiantamento, diaadiantamento, diapagamentofinal, nomeusuario) ' +
                'VALUES (:vsal, :fparc, :perad, :diaadiant, :diapag, :nome)';

      Qry.ParamByName('vsal').AsFloat := 0;
      Qry.ParamByName('fparc').AsBoolean := False;
      Qry.ParamByName('perad').AsFloat := 0;
      Qry.ParamByName('diaadiant').AsInteger := 0;
      Qry.ParamByName('diapag').AsInteger := 0;
      Qry.ParamByName('nome').AsString := '';
      Qry.ExecSQL;
      Result := True;
    end
    else
    begin
      Exit
    end;
  finally
    Qry.Free;
  end;
end;

function TConfigGeralDAO.AlterarConfiguracao(AColuna: String; ANovoValor: Variant): Boolean;
var
  Qry: TFDQuery;
begin
  Result := False;
  Qry := TFDQuery.Create(nil);

  try
    Qry.Connection := dmConexao.FDConnection;
    Qry.SQL.Text := 'UPDATE configuracao SET ' + AColuna + ' = :Valor WHERE id = 1';
    Qry.ParamByName('Valor').Value := ANovoValor;
    Qry.ExecSQL;

    Result := True;
  finally
    Qry.Free;
  end;
end;

class function TConfigGeralDAO.UpdateInstanciaConfig: TConfigGeral;
var
   Qry: TFDQuery;
   Config: TConfigGeral;
begin
  Result := nil;
  Qry := TFDQuery.Create(nil);

  try
    Qry.Connection := dmConexao.FDConnection;
    Qry.SQL.Text := 'SELECT * FROM configuracao LIMIT 1';
    Qry.Open;

    if not Qry.IsEmpty then
    begin
      Config := TConfigGeral.Create;
      Config.ValorSalario := Qry.FieldByName('ValorSalario').AsFloat;
      Config.FlagParcelado := Qry.FieldByName('FlagParcelado').AsBoolean;
      Config.PercentualAdiantamento := Qry.FieldByName('PercentualAdiantamento').AsFloat;
      Config.DiaAdiantamento := Qry.FieldByName('DiaAdiantamento').AsInteger;
      Config.DiaPagamentoFinal := Qry.FieldByName('DiaPagamentoFinal').AsInteger;
      Config.NomeUsuario := Qry.FieldByName('NomeUsuario').AsString;

      Result := Config;
    end
    else
    begin
      Result := TConfigGeral.Create;
    end;
  finally
    Qry.Free;
  end;
end;

end.
