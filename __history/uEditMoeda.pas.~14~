unit uEditMoeda;

interface

uses
  System.SysUtils, System.Classes, FMX.Edit, FMX.Types, FMX.Controls;

type
  TEditMoeda = class(TEdit)
  private
    FInChange: Boolean;
    procedure HandleTyping(Sender: TObject);
    procedure FormatarTextoComoMoeda;
    function GetValor: Double;
    procedure SetValor(AValue: Double);
  protected
    procedure DoEnter; override;
  public
    constructor Create(AOwner: TComponent); override;
    property AsFloat: Double read GetValor write SetValor;
  end;

procedure Register;

implementation

{ TEditMoeda }

procedure Register;
begin
  RegisterComponents('Savr', [TEditMoeda]);
end;

constructor TEditMoeda.Create(AOwner: TComponent);
begin
  inherited;
  KeyboardType := TVirtualKeyboardType.NumberPad;
  FilterChar := '0123456789';
  OnTyping := HandleTyping;
end;

function TEditMoeda.GetValor: Double;
var
  TextoLimpo: string;
begin
  TextoLimpo := StringReplace(Text, 'R$', '', [rfReplaceAll]);
  TextoLimpo := StringReplace(TextoLimpo, '.', '', [rfReplaceAll]);
  TextoLimpo := StringReplace(TextoLimpo, ',', '.', [rfReplaceAll]);
  TextoLimpo := StringReplace(TextoLimpo, ' ', '', [rfReplaceAll]);

  Result := StrToFloatDef(TextoLimpo, 0);
end;

procedure TEditMoeda.SetValor(AValue: Double);
begin
  Text := FormatFloat('R$ #,##0.00', AValue);
end;

procedure TEditMoeda.DoEnter;
begin
  inherited;
  SelectAll;
end;

procedure TEditMoeda.HandleTyping(Sender: TObject);
begin
  if not FInChange then
    FormatarTextoComoMoeda;
end;

procedure TEditMoeda.FormatarTextoComoMoeda;
var
  TextoLimpo: string;
  V: Currency;
begin
  FInChange := True;
  try
    TextoLimpo := Text.Replace('R$', '', [rfReplaceAll]);
    TextoLimpo := TextoLimpo.Replace('.', '', [rfReplaceAll]);
    TextoLimpo := TextoLimpo.Replace(',', '', [rfReplaceAll]);
    TextoLimpo := Trim(TextoLimpo);

    if TextoLimpo = '' then
      TextoLimpo := '0';

    if TryStrToCurr(TextoLimpo, V) then
      Text := FormatFloat('"R$" #,##0.00', V / 100)
    else
      Text := 'R$ 0,00';

    CaretPosition := Length(Text);
    SetValor(GetValor);
  finally
    FInChange := False;
  end;
end;

end.

